buildscript {
    ext {
        springBootVersion = '1.5.21.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'java'
    id 'application'
}

group 'com.theopus'
version '1.0-SNAPSHOT'
mainClassName = "com.theopus.App"
sourceCompatibility = 1.8



repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'java'
    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }

    }

    compileJava.options.encoding = 'UTF-8'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

dependencies {
    compile project(":schedule-backend")
    compile project(":schedule-rest")
    compile project(":schedule-telegram")
    compile project(":schedule-web")
    compile project(":server")
}

distTar {
    into("${project.name}-${version}/conf") {
        from "server/src/main/resources/"
        include "application.yml"
        filter { it.replaceAll("classpath:", "../conf/") }
        rename('application.yml', "config.yml")
    }
    into("${project.name}-${version}/lib") {
        from "schedule-backend/src/main/resources/"
        include "*"
    }
    compression = Compression.GZIP
}

task startScript(type: CreateStartScripts) {
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    mainClassName = 'com.theopus.App'
    applicationName = 'knuca-schedule-start'
    defaultJvmOpts = ["-Xms64m", "-Xmx128m", "-Dspring.config.location=../conf/config.yml"]
}

applicationDistribution.into("bin") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(startScript)
    fileMode = 0755
}

startScripts {
    doLast {
        windowsScript.delete()
        unixScript.delete()
    }
}

task copyDistribution (type: Copy){
    from distTar
    into file("docker/")
    rename("${project.name}-${version}.tgz", "knuca-schedule.tgz")
}

build.finalizedBy {
    copyDistribution
}
